#!/usr/bin/env gnuplot

set term postscript enhanced color font "Helvetica,20"
set encoding iso_8859_1

nrep = 3
array conc[nrep] = [5, 10, 30]

nsys = 4
array titles[nsys] = ['Axolemma', 'CHL1/POPC/PSM', 'Hydroxyapatite', 'EE_{40}EO_{37}']
array systems[nsys] = ['axolemma', 'membrane', 'ha', 'peo']

nd = 2
array dims[nd] = ['x', 'z']
array dlabels[nd] = ['X', 'Z']

set xlabel "Time (ns)" font ",24"
set tics font ",22"

infile(sys, r) = sprintf("../%s_%d/analysis/sys_info.plo", systems[sys], conc[r])
ltitle(r) = sprintf("%d%% v/v", conc[r])

do for [d=1:nd] {
    set ylabel sprintf("%s ({\305})", dlabels[d]) font ",24"

    do for [sys=1:nsys] {
        if (d == 2 && systems[sys] eq "membrane") {
            set key right center
        }

        set title titles[sys] font ",28"
        set output sprintf("sysinfo/output/%s_%s.eps", systems[sys], dims[d])
        plot for [r=1:nrep] infile(sys, r) u 1:(column(2+d)) w l lw 3 t ltitle(r)

        set key top right
    }
}


nd = 3
array dims[nd] = ['x', 'y', 'z']
array dlabels[nd] = ['X', 'Y', 'Z']

do for [d=1:nd] {
    set ylabel sprintf("%s ({\305})", dlabels[d]) font ",24"

    if (d != 3) {
        set key right center
    }

    # step 6.1 ends
    set arrow from 0.125, graph 0 to 0.125, graph 1 nohead
    # step 6.2 ends
    set arrow from 0.250, graph 0 to 0.250, graph 1 nohead
    # step 6.3 ends
    set arrow from 0.375, graph 0 to 0.375, graph 1 nohead
    # step 6.4 ends
    set arrow from 0.875, graph 0 to 0.875, graph 1 nohead
    # step 6.5 ends
    set arrow from 1.375, graph 0 to 1.375, graph 1 nohead
    # step 6.6 ends

    set title "Mica + POPC" font ",28"
    set output sprintf("sysinfo/output/mcm%s.eps", dims[d])
    plot for [r=1:nrep] sprintf("../mcm%d/analysis/sys_equi.plo", r) \
             u 1:(column(1+d)) w l lw 3 t sprintf("%d nm", r)

    set key top right
    unset arrow
}

