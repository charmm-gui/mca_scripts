#!/usr/bin/env gnuplot

set term postscript enhanced color font "Helvetica,14"
set encoding utf8

f(i) = sprintf("msd/mcm%d_diffusion_bot_250.csv", i)
#f(i) = sprintf("msd/mcm%d_diffusion_bot_500.csv", i)
g(i) = sprintf("msd/mcm%d_diffusion_top_250.csv", i)

# diffusion constant calculated from least squares fit
D(i) = system(sprintf("grep Fit %s | sed -E 's/^.*: *([0-9.-]+) ±.*/\\1/'", f(i)))
Dt(i) = system(sprintf("grep Fit %s | sed -E 's/^.*: *([0-9.-]+) ±.*/\\1/'", g(i)))

# diffusion plus or minus error
Dpe(i) = system(sprintf("grep Fit %s | sed -E 's/^.*: *([0-9.-]+ ± [0-9.-]+).*/\\1/'", f(i)))
Dtpe(i) = system(sprintf("grep Fit %s | sed -E 's/^.*: *([0-9.-]+ ± [0-9.-]+).*/\\1/'", g(i)))

# y-intercept of the line that was least squares fit
b(i) = system(sprintf("grep Fit %s | sed -E 's/.*intercept ([0-9.-]+).*/\\1/'", f(i)))
bt(i) = system(sprintf("grep Fit %s | sed -E 's/.*intercept ([0-9.-]+).*/\\1/'", g(i)))

bpe(i) = system(sprintf("grep Fit %s | sed -E 's/.*intercept ([0-9.-]+ ± [0-9.-]+).*/\\1/'", f(i)))
btpe(i) = system(sprintf("grep Fit %s | sed -E 's/.*intercept ([0-9.-]+ ± [0-9.-]+).*/\\1/'", g(i)))

do for [i=1:11] {
    set linetype i linewidth 3
}

set key left font ",20"
set tics font ",22"
set size square

set ylabel "MSD (Å^2)" font ",24" offset -1,0
set xlabel "{/Symbol t} (ns)" font ",24"
set xrange [0:]
set yrange [0:]

nl = 3
array DL[nl]
array err_DL[nl]
do for [i=1:nl] {
    before_after = Dpe(i)
    before = system(sprintf("echo '%s' | sed -E 's/([0-9.-]+) ± ([0-9.-]+)/\\1/g'", before_after))
    after  = system(sprintf("echo '%s' | sed -E 's/([0-9.-]+) ± ([0-9.-]+)/\\2/g'", before_after))
    DL[i] = before*1
    err_DL[i] = after*1
}

set title "POPC Displacement in Bottom Leaflet" font ",28"
set output "msd/figs/msd_bot.eps"
plot for [i=1:3] f(i) every 75 with points t "", \
     for [i=1:3] D(i)*4*x + b(i) w l ls i t "", \
     for [i=1:3] f(i) every 10000 w lp ls i \
         t sprintf('D_{%d nm} = %.2f (Å^2/ns)', i, DL[i])
         #t sprintf('D_{%d nm} = %.2f ± %.2f (Å^2/ns)', i, DL[i], err_DL[i])

do for [i=1:nl] {
    before_after = Dtpe(i)
    before = system(sprintf("echo '%s' | sed -E 's/([0-9.-]+) ± ([0-9.-]+)/\\1/g'", before_after))
    after  = system(sprintf("echo '%s' | sed -E 's/([0-9.-]+) ± ([0-9.-]+)/\\2/g'", before_after))
    DL[i] = before*1
    err_DL[i] = after*1
}

set title "POPC Displacement in Top Leaflet" font ",28"
set output "msd/figs/msd_top.eps"
plot for [i=1:3] g(i) every 75 with points t "", \
     for [i=1:3] Dt(i)*4*x + bt(i) w l ls i t "", \
     for [i=1:3] g(i) every 10000 w lp ls i \
         t sprintf('D_{%d nm} = %.2f (Å^2/ns)', i, DL[i])
         #t sprintf('D_{%d nm} = %.2f ± %.2f (Å^2/ns)', i, DL[i], err_DL[i])
