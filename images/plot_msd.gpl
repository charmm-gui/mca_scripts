#!/usr/bin/env gnuplot

set term postscript enhanced color font "Helvetica,14"
set encoding utf8

# diffusion constant calculated from least squares fit
D(filename) = system(sprintf("grep Fit %s | sed -E 's/^.*: *([0-9.-]+) ±.*/\\1/'", filename))

# diffusion plus or minus error
Dpe(filename) = system(sprintf("grep Fit %s | sed -E 's/^.*: *([0-9.-]+ ± [0-9.-]+).*/\\1/'", filename))

# y-intercept of the line that was least squares fit
b(filename) = system(sprintf("grep Fit %s | sed -E 's/.*intercept ([0-9.-]+).*/\\1/'", filename))
bpe(filename) = system(sprintf("grep Fit %s | sed -E 's/.*intercept ([0-9.-]+ ± [0-9.-]+).*/\\1/'", filename))

replica=`pwd | sed -E "s#.*/[^0-9]*([0-9]*)/.*#\1#"`
set title sprintf("POPC Displacement (%d nm gap)", replica) font ",24"
set key font ",18"
set tics font ",18"

set ylabel "MSD (Å^2)" font ",20"
set xlabel "{/Symbol t} (ns)" font ",20"
set xrange [0:]
set yrange [0:]

d1  = D("diffusion_top.csv")
de1 = Dpe("diffusion_top.csv")
i1  = b("diffusion_top.csv")
d2  = D("diffusion_bot.csv")
de2 = Dpe("diffusion_bot.csv")
i2  = b("diffusion_bot.csv")

set output "msd.eps"
plot "diffusion_top.csv" every 100 with linespoints title "top leaflet MSD", \
     d1*4*x + i1 w l t sprintf('top leaflet D = %s', de1), \
     "diffusion_bot.csv" every 100 with linespoints title "bottom leaflet MSD", \
     d2*4*x + i2 w l t sprintf('bottom leaflet D = %s', de2)
